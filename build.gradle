defaultTasks "run" // gradle とコマンドすると gradle run になる

buildscript {
    ext.kotlin_version = "1.0.6"
    ext.gradle_version = "3.3"
    ext.junit_version  = "4.12"

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "kotlin"
apply plugin: "application"

mainClassName = "hatonekoe.MainKt"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "no.tornado:tornadofx:1.6.1"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "junit:junit:$junit_version"
}

test {
    testLogging {
        exceptionFormat = "full" // fail時に詳細を表示
    }
}

/* jar ファイルを /build/libs 下に作成 */
task createJar(type: Jar, dependsOn: jar) {
    archiveName = "output.jar"
    manifest {
        attributes "Main-Class" : mainClassName
    }

    from configurations.compile.collect {it.isDirectory() ? it : zipTree(it)}
    from "$buildDir/classes/main"
    from "$buildDir/resources/main"
}

task wrapper(type: Wrapper) {
    gradleVersion = "$gradle_version"
}
